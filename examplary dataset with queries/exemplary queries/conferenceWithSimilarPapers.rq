PREFIX dt: <https://w3id.org/rdf-tensor/datatypes#>
PREFIX dtf: <https://w3id.org/rdf-tensor/functions#>
PREFIX dta: <https://w3id.org/rdf-tensor/aggregators#>
PREFIX ocs_papers: <https://w3id.org/ocs/ont/papers/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX fabio: <http://purl.org/spar/fabio/>
PREFIX schema: <http://schema.org/>
PREFIX fn: <http://www.w3.org/2005/xpath-functions#>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>

SELECT ?conferenceName (fn:round(?min, 3) AS ?score)  ?counted
WHERE {
    {
        SELECT ?conference ?conferenceName (dta:std(?o) AS ?stdEmbedding) (COUNT(?o) AS ?counted)
        WHERE {
            ?paper a fabio:ResearchPaper ;
                ocs_papers:hasWordEmbedding ?o ;
                frbr:realization ?realization .

            ?realization a fabio:ConferencePaper ;
                frbr:partOf ?conference .

            ?conference a fabio:ConferenceProceedings ;
                schema:name ?conferenceName .
        }
        GROUP BY ?conference ?conferenceName
    }
    BIND(dtf:min(-1, ?stdEmbedding) AS ?min)
}
ORDER BY DESC(?min)
LIMIT 5