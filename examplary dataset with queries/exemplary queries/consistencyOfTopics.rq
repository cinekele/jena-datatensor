PREFIX dt: <https://w3id.org/rdf-tensor/datatypes#>
PREFIX dtf: <https://w3id.org/rdf-tensor/functions#>
PREFIX dta: <https://w3id.org/rdf-tensor/aggregators#>
PREFIX ocs_papers: <https://w3id.org/ocs/ont/papers/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX fabio: <http://purl.org/spar/fabio/>
PREFIX schema: <http://schema.org/>
PREFIX fn: <http://www.w3.org/2005/xpath-functions#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT ?concept_name (fn:round(?norm1, 3) AS ?normOfEmbedding) ?counted
WHERE {
    
    {
        SELECT ?concept ?concept_name (dta:var(?o) AS ?stdEmbedding) (COUNT(?o) AS ?counted)
        WHERE {
            ?paper a fabio:ResearchPaper ;
                ocs_papers:hasWordEmbedding ?o ;
                ocs_papers:hasRelatedTopic ?topic .

            ?topic ocs_papers:hasRelationTarget ?concept .

            ?concept skos:prefLabel ?concept_name .
        }
        GROUP BY ?concept ?concept_name
        HAVING (?counted > 5)
    }

    BIND(dtf:norm1(-1, ?stdEmbedding) AS ?norm1)
}
ORDER BY ASC(?norm1)
LIMIT 5